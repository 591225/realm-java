buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

version = new File("${projectDir}/../version.txt").text
sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories {
    jcenter()
}

dependencies {
    compile group:'com.squareup', name:'javawriter', version:'2.5.0'
    compile files("../realm-annotations/build/libs/realm-annotations-${version}.jar")
    testCompile files('../realm/build/intermediates/bundles/debug/classes.jar')
    testCompile files("${System.properties['java.home']}/../lib/tools.jar") // This is needed otherwise compile-testing won't be able to find it
    testCompile group:'junit', name:'junit', version:'4.11'
    testCompile group:'com.google.testing.compile', name:'compile-testing', version:'0.6'
}

jar {
    from configurations.compile.findAll {it.name.contains('javawriter')}.collect { zipTree(it) }
}

shadowJar {
    relocate 'com.squareup.javawriter', 'io.realm.processor.javawriter'
}

task compileAnnotations(type: GradleBuild) {
    dir = file('../realm-annotations')
    tasks = ['assemble']
}

task assembleRealm(type: GradleBuild) {
    dir = file('../realm')
    tasks = ['assemble']
}

tasks.compileJava {
    dependsOn compileAnnotations
}

tasks.check {
    dependsOn assembleRealm
}